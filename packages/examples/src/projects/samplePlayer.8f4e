8f4e/v1

config project
; @pos -53 -63

rescope "exportFileName"
set "samplePlayer"

rescope "memorySizeBytes"
set 655360
popScope

scope "runtimeSettings"
scope "sampleRate"
push 44100
; push 22050
set

rescopeTop "runtime"
set "AudioWorkletRuntime"

rescopeTop "audioOutputBuffers[0]"

scope "memoryId"
set "audioout.buffer"

rescopeTop "channel"
set 0

rescopeTop "output"
set 0

rescope "runtimeSettings"
scope "audioOutputBuffers[1]"

scope "memoryId"
set "audioout.buffer"

rescopeTop "channel"
set 1

rescopeTop "output"
set 0

popScope
popScope
popScope
popScope

configEnd

module notes
; @pos 33 -45
; Loads an external 8-bit PCM asset into memory, 
; continuously plays it with a looping playhead, 
; and routes the normalized output into a single 
; audioout buffer that is read by both left and 
; right audio channels.
moduleEnd

module assets
; @pos -107 -70
; @defAsset amen https://static.llllllllllll.com/andor/8f4e/amen_170bpm_8bit_unsigned.pcm

moduleEnd

module audioout
; @pos 130 -63
use env

float* in &pcmPlayer8bit.out

float[] buffer AUDIO_BUFFER_SIZE
int pointer &buffer

; @plot buffer -1 1

; Store the input value
; in the buffer
push pointer
push *in
store

; Increment the buffer 
; pointer by the word size
; which is 4 bytes
push &pointer ; address
push pointer
push %buffer  ; word size
add
store

; Reset the buffer pointer
; when it reaches the end
; of the buffer
push pointer
push buffer&  ; end address
greaterThan 
if void
push &pointer ; address
push &buffer  ; address
store
ifEnd

moduleEnd

constants env
; @pos 32 -63
; @favorite
; Auto-generated environment constants
; Changes will be overwritten
; Last updated: 2/26/2026, 1:22:04 PM

const SAMPLE_RATE 44100
const INV_SAMPLE_RATE 0.000022675736961451248
const AUDIO_BUFFER_SIZE 128

; Binary asset sizes in bytes
; 'amen_170bpm_8bit_unsigned.pcm'
const ASSET_AMEN_SIZE 62258

constantsEnd

module pcmPlayer8bit
; @pos 91 -63
; @home
use env

int8u[] buffer ASSET_AMEN_SIZE
; @loadAsset amen &buffer
int playhead &buffer
float out
int startPos 0
int endPos ASSET_AMEN_SIZE

; @scan buffer playhead

; Read sample at playhead
; write it to output
push &out
push playhead
load8u
castToFloat
push ^buffer ; max value
castToFloat
div
push 2.0
mul
push 1.0
sub
store

; Advance the playhead
push &playhead
push playhead
push %buffer ; word size
add
store

; Reset playhead
; when reaches buffer end
push playhead
push &buffer
push endPos
add
greaterThan
if void
push &playhead
push &buffer
push startPos
add
store
ifEnd

moduleEnd
