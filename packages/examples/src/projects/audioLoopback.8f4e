8f4e/v1

config project
; @pos -106 13

rescope "runtimeSettings"
scope "audioOutputBuffers[0]"

scope "memoryId"
push "audiooutL.buffer"
set

rescopeTop "channel"
push 0
set

rescopeTop "output"
push 0
set

rescope "runtimeSettings"
scope "audioOutputBuffers[1]"

scope "memoryId"
push "audiooutR.buffer"
set

rescopeTop "channel"
push 1
set

rescopeTop "output"
push 0
set

configEnd

config project
; @pos -106 -10

rescope "runtimeSettings"
scope "audioInputBuffers[0]"

scope "memoryId"
push "audioin.buffer"
set

rescopeTop "channel"
push 0
set

rescopeTop "input"
push 0
set

configEnd

comment
; @pos 130 12
; This is a stereo loopback example
; that reads input audio from a 
; shared buffer and writes the same 
; signal into left and right output 
; buffers using circular-buffer 
; pointer logic.
commentEnd

config project
; @pos -141 4

rescope "memorySizeBytes"
push 65536
set

configEnd

config project
; @pos -141 19

rescope "runtimeSettings"

scope "sampleRate"
push 44100
set

rescopeTop "runtime"
push "AudioWorkletRuntime"
set

rescope "exportFileName"
push "audioLoopback"
set

configEnd

module audioin
; @pos 5 1
use env

; Audio buffer
float[] buffer AUDIO_BUFFER_SIZE
float out
int pointer &buffer

; @plot buffer -2 2

push &out
push pointer
loadFloat
store

push &pointer 
push pointer
push %buffer
add
store

push pointer
push buffer&
greaterThan
if void
push &pointer
push &buffer
store
ifEnd

moduleEnd

module audiooutL
; @pos 48 1
; @home
use env

float* in &audioin.out

; Audio buffer
float[] buffer AUDIO_BUFFER_SIZE
int pointer &buffer

; @plot buffer -2 2

; Store the input value
; in the buffer
push pointer
push *in
store

; Increment the buffer 
; pointer by the
; buffer word size
push &pointer
push pointer
push %buffer
add
store

; Reset the buffer pointer
; when it reaches the end
; of the buffer
push pointer
push buffer&
greaterThan
if void
push &pointer
push &buffer
store
ifEnd

moduleEnd

module audiooutR
; @pos 89 1

use env

float* in &audioin.out

; Audio buffer
float[] buffer AUDIO_BUFFER_SIZE
int pointer &buffer

; @plot buffer -2 2

; Store the input value
; in the buffer
push pointer
push *in
store

; Increment the buffer 
; pointer by the
; buffer word size
push &pointer
push pointer
push %buffer
add
store

; Reset the buffer pointer
; when it reaches the end
; of the buffer
push pointer
push buffer&
greaterThan
if void
push &pointer
push &buffer
store
ifEnd

moduleEnd

constants env
; @pos -52 1
; @favorite
; Auto-generated environment constants
; Changes will be overwritten
; Last updated: 2/19/2026, 1:30:23 PM

const SAMPLE_RATE 44100
const INV_SAMPLE_RATE 0.000022675736961451248
const AUDIO_BUFFER_SIZE 128

constantsEnd